apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "com.jaredsburrows.license"

def keystorePropertiesFile = rootProject.file("app/keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    apply plugin: 'io.fabric'
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resConfigs("en", "ru", "uk")
        manifestPlaceholders = [
                fabricApiKey: keystoreProperties['FabricApiKey'] != null
                        ? keystoreProperties['FabricApiKey']
                        : ""
        ]
        multiDexKeepFile file('multidex-keep.txt')
    }

    buildTypes {
        debug {
            apply from: "app_config.gradle"
            if (file("dev_app_config.gradle").exists()) {
                apply from: "dev_app_config.gradle"
            }
            applyAppConfig(android.buildTypes.debug)

            signingConfig signingConfigs.debug
            debuggable true
        }

        staging {
            apply from: "app_config.gradle"
            if (file("stage_app_config.gradle").exists()) {
                apply from: "stage_app_config.gradle"
            }
            applyAppConfig(android.buildTypes.staging)

            signingConfig signingConfigs.debug
            debuggable true
        }

        release {
            apply from: "app_config.gradle"
            if (file("release_app_config.gradle").exists()) {
                apply from: "release_app_config.gradle"
            }
            applyAppConfig(android.buildTypes.release)

            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = false
    }

    licenseReport {
        generateHtmlReport = true
        generateJsonReport = false
        copyHtmlReportToAssets = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

ext {
    anko_version = '0.10.5'
    support_lib_version = '28.0.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation "com.android.support:appcompat-v7:${support_lib_version}"
    implementation "com.android.support:recyclerview-v7:${support_lib_version}"
    implementation "com.android.support:support-annotations:${support_lib_version}"
    implementation "com.android.support:cardview-v7:${support_lib_version}"
    implementation "com.android.support:design:${support_lib_version}"
    implementation "com.android.support:preference-v14:${support_lib_version}"
    implementation 'com.google.android.gms:play-services-base:16.1.0'
    implementation "com.android.support:support-v4:${support_lib_version}"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:design:$support_lib_version"

    implementation 'org.jetbrains.anko:anko-sdk15:0.9.1'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'
    implementation("com.mikepenz:materialdrawer:6.0.9@aar") { transitive = true }
    implementation 'com.journeyapps:zxing-android-embedded:3.0.2@aar'
    implementation 'com.google.zxing:core:3.2.1'
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.google.dagger:dagger:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.13'
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation "org.tokend:sdk:2.6.1-x.8"
    implementation "org.tokend:rx-sdk:2.0.0"

    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.1.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'ru.noties:markwon:1.0.1'
    implementation 'ru.noties:markwon-image-loader:1.0.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true;
    }
    implementation 'com.github.clans:fab:1.6.4'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.marcelkliemannel:kotlin-onetimepassword:1.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
